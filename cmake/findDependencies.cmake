if (BUILD_GUI)
    if (NOT WITH_QCHART)
        find_package(Qt5 COMPONENTS Core Gui Widgets PrintSupport LinguistTools REQUIRED)
    else()
        find_package(Qt5 COMPONENTS Core Gui Widgets PrintSupport LinguistTools Charts REQUIRED)
    endif()
endif()

if (HAVE_RULES)
    find_library(PCRE pcre)
    if (NOT PCRE)
        message(FATAL_ERROR "pcre dependency for RULES has not been found")
    endif()
endif()

if (USE_Z3)
    find_library(Z3_LIBRARY z3)
    if (NOT Z3_LIBRARY)
        message(FATAL_ERROR "Z3 dependency has not been found")
    endif()
    find_path(Z3_INCLUDE z3++.h PATH_SUFFIXES "z3")
    if (NOT Z3_INCLUDE)
        message(FATAL_ERROR "z3++.h has not been found")
    endif()
    include_directories(${Z3_INCLUDE})
endif()

set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(CMAKE_AUTOMOC OFF)

if (NOT USE_MATCHCOMPILER_OPT MATCHES "Off")
    find_package(PythonInterp)
    if (NOT ${PYTHONINTERP_FOUND})
        message(WARNING "No python interpreter found. Therefore, the match compiler is switched off.")
        set(USE_MATCHCOMPILER_OPT "Off")
    endif()
endif()
